buildscript {
	repositories {
		mavenCentral()
		maven { url "https://maven.fabricmc.net" }
		maven { url "https://repo.sleeping.town" } //Commented out to use the version of voldeloom in the settings.gradle includeBuild (hopefully??)
	}
	dependencies {
		classpath "agency.highlysuspect:voldeloom:2.4-SNAPSHOT"
	}
}

apply plugin: "agency.highlysuspect.voldeloom"

java {
	toolchain {
		//Last version able to set a --release as low as 6.
		languageVersion = JavaLanguageVersion.of(11)
	}
}

compileJava {
	//Forge doesn't understand classes compiled to versions of the class-file format newer than Java 6's.
	options.release.set(6)
}

String minecraftVersion = project.minecraft
String forgeVersion = project.minecraft_forge
archivesBaseName = project.modid
version = project.modVersion

volde {
	runs {
		client {
			programArg "CrossVas"
		}
	}
}

repositories {
	maven {
		url = "https://api.modrinth.com/maven"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	forge "net.minecraftforge:forge:${forgeVersion}:universal@zip"
	mappings "net.minecraftforge:forge:${forgeVersion}:src@zip"

	implementation 'com.google.code.gson:gson:2.11.0'

	modImplementation files("libs/VintageCore-1.5.2-2.0.1-dev.jar")
	modImplementation files("libs/BlockHelperAddons-1.5.2-2.0.0-beta6-dev.jar")
	modImplementation files("libs/industrialcraft-2_1.117.385-lf.jar")
	modImplementation files("libs/craftguide.zip")
}

def props = ['modName', 'modid', 'modVersion', 'modDescription', 'modAuthors', 'modDeps', 'minecraft', 'url']
processResources {
	filesMatching("mcmod.info") {
		expand(project.properties.subMap(props))
	}
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

jar {
	from('compat') {
		include 'reforged/ic2/addons/asp/compat/**'
	}
}